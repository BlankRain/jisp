;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; Dependencies ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(= gulp   (require "gulp")
   plug   ((require "gulp-load-plugins"))
   brsync (require "browser-sync")
   reload brsync.reload)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; Utilities ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(def handle stream
  (stream.on "error" (fn err (do
    (plug.util.log err)
    (plug.util.log "\x07\x07\x07\x07\x07")
    (stream.end)))))

(mac task name pre ...args (do
  (if (and (not (Array.isArray pre)) (? pre))
    (do (args.unshift pre)
        (= pre ``())))
  (if (is args.length 0)
    (= pipeline `())
    (do (= pipeline `(do (handle (,(args.shift) ,(args.shift)))))
        (while (> args.length 0) (do
          (= left  (args.shift)
             right (args.shift))
          (pipeline.push `(.pipe (handle (,left ,right))))))))
  `(gulp.task ,name ,pre (fn ,pipeline))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; Tasks ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Less
(task         "less"
  gulp.src    "src/less/app.less"
  plug.less   ()
  gulp.dest   "css/"
  reload      (stream: yes))

; Jisp-browser
(task         "jisp-browser"
  gulp.src    "node_modules/jisp/browser/jisp.js"
  gulp.dest   "js/")

; JSX
(task        "jsx"
  gulp.src   "src/jsx/*.jsx"
  plug.react (harmony: yes)
  gulp.dest  "src/tmp/js/")

; Static server
(gulp.task "browser-sync" (fn
  (brsync (server: (baseDir: "./") port: 3400 online: no))))

; Watch
(gulp.task "watch" (fn (do
  (gulp.watch "src/less/*.less" `("less"))
  (gulp.watch "src/jsx/*.jsx"   `("jsx"))
  (gulp.watch "js/*.js"         {reload}))))

; Default
(task "default" `("less" "jisp-browser" "jsx" "watch" "browser-sync"))
