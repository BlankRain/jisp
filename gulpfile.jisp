(= gulp   (require "gulp")
   plug   ((require "gulp-load-plugins"))
   brsync (require "browser-sync")
   reload brsync.reload
   marked (require "marked"))

(= jsFiles `(
  "bower_components/angular/angular.js"
  "bower_components/angular-strap/dist/angular-strap.js"
  "bower_components/angular-strap/dist/angular-strap.tpl.js"
  "bower_components/angular-animate/angular-animate.js"
  "bower_components/js-beautify/js/lib/beautify.js"
  "bower_components/jquery/dist/jquery.min.js"
))

; configure markdown converter
(marked.setOptions
  (gfm:         yes
   tables:      yes
   breaks:      yes
   sanitize:    no
   smartypants: yes))

(def handle stream
  (stream.on "error" (fn err (do
    (plug.util.log err)
    (plug.util.log "\x07\x07\x07\x07\x07")
    (stream.end)))))

(mac task name pre ...args (do
  (if (and (not (Array.isArray pre)) (? pre))
    (do (args.unshift pre)
        (= pre ``())))
  (if (is args.length 0)
    (= pipeline `())
    (do (= pipeline `(do (handle (,(args.shift) ,(args.shift)))))
        (while (> args.length 0) (do
          (= left  (args.shift)
             right (args.shift))
          (pipeline.push `(.pipe (handle (,left ,right))))))))
  `(gulp.task ,name ,pre (fn ,pipeline))))

; less
(task         "less"
  gulp.src    "src/less/app.less"
  plug.less   ()
  gulp.dest   "css/"
  reload      (stream: yes))

; js
(task         "js"
  gulp.src    jsFiles
  plug.concat "deps.js"
  plug.uglify (mangle: no)  ; takes forever
  gulp.dest   "js/tmp/")

; jisp
(task         "jisp"
  gulp.src    "src/jisp/*.jisp"
  plug.concat "app-jisp.jisp"
  plug.jisp   ()
  plug.uglify (mangle: no)  ; takes forever
  gulp.dest   "js/tmp/")

; jisp-browser
(task         "jisp-browser"
  gulp.src    "node_modules/jisp/browser/jisp.js"
  gulp.dest   "js/")

; scripts
(task         "scripts" `("js" "jisp" "jisp-browser")
  gulp.src    `("js/tmp/deps.js" "js/tmp/app-jisp.js")
  plug.concat "app.js"
  gulp.dest   "js/")

; scripts-mini
(task         "scripts-mini" `("jisp")
  gulp.src    `("js/tmp/deps.js" "js/tmp/app-jisp.js")
  plug.concat "app.js"
  gulp.dest   "js/"
  reload      (stream: yes))

; jade
(task         "jade"
  gulp.src    "src/jade/index.jade"
  plug.jade   (pretty: yes)
  gulp.dest   ""
  reload      (stream: yes))

; static server
(gulp.task "browser-sync" (fn
  (brsync (server: (baseDir: "./") port: 3400))))

; watch files for changes
(gulp.task "watch" (fn (do
  (gulp.watch "src/jisp/*.jisp"      `("scripts-mini"))
  (gulp.watch "src/less/*.less"      `("less"))
  (gulp.watch "src/jade/*.jade"      `("jade")))))

; default
(task "default" `("less" "scripts" "jade" "watch" "browser-sync"))
