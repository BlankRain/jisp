; Mostly copied from CoffeeScript 1.7.1 source

(= child_process (require "child_process")
   path          (require "path")
   util          (require "./util")
   jisp          (require "./jisp"))

; Load and run a jisp file for Node, stripping `BOM`s
(def loadFile module filename
     (module._compile (jisp.compileFile filename) filename))

; If the installed version of Node supports `require.extensions`, register as an extension
(if require.extensions
    (for ext jisp.fileExtensions
         (= require.extensions[ext] loadFile)))

; If we're on Node, patch `child_process.fork` so our scripts can fork both jisp and JS files
; Not sure if this is relevant for jisp
(if child_process
    (= fork   child_process.fork
       binary (require.resolve "../../bin/jisp")
       child_process.fork
         (fn path args options
             (do (if (util.isJisp path)
                     (do (if (not (Array.isArray args))
                             (= options (or args (:))
                                args    `()))
                         (= args ((get `(path) concat) args)
                            path binary)))
                 (fork path args options)))))
