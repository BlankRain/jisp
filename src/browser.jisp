; Browser compatibility module
; Copied from CoffeeScript source
; ToDo add polyfill
; ToDo add source map support

(= jisp         (require "./jisp")
   jisp.require require
   compile      jisp.compile)

; Use standard JS `eval` to eval code
(= jisp.eval (fn code (options (:)) (do
  (= options.wrap no)
  (eval (compile code options)))))

(= jisp.run (fn code (options (:)) (do
  (= options.wrap no)
  (= compiled (compile code options))
  ((Function (compile code options))))))

; If we're not in a browser environment, end the script
(if (?! window) (return))

; Load remote script by XHR
(= jisp.load (fn url callback (options (:)) (hold no) (do
  (= options.sourceFiles `(url)
     xhr (if window.ActiveXObject
             (new window.ActiveXObject "Microsoft.XMLHTTP")
             (new window.XMLHttpRequest)))
  (xhr.open "GET" url yes)
  (if (of `overrideMimeType xhr) (xhr.overrideMimeType "text/plain"))
  (= xhr.onreadystatechange (fn (do
    (if (is xhr.readyState 4)
        (if (is xhr.status 0 200)
            (do (= param `(xhr.responseText options))
                (if (not hold) (jisp.run ...param)))
            (throw (new Error (+ "Could not load " url)))))
    (if callback (callback param)))))
  (xhr.send null))))

; Compile and eval all `text/jisp` script tags
; Happens on page load
(def runScripts (do
  (= scripts (window.document.getElementsByTagName "script")
     jisps   `()
     index   0)
  (for s scripts
    (if (is s.type "text/jisp") (jisps.push s)))
  (def execute (do
    (= param jisps[index])
    (if (instanceof param Array)
      (do (jisp.run ...param)
          (++ index)
          (execute)))))
  (for script i jisps (let script script i i (do
    (= options (:))
    (if script.src
      (jisp.load script.src (fn param
        (do (= jisps[i] param)
            (execute)))
        options
        true)
      (do (= options.sourceFiles `("embedded"))
          (= jisps[i] `(script.innerHTML options)))))))
  (execute)))

; Listen for window load: decent browsers / IE
(if window.addEventListener
  (window.addEventListener "DOMContentLoaded" runScripts no)
  (window.attachEvent "onload" runScripts))
